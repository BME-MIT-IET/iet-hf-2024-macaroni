plugins {
    id 'java-library' 
    id 'jacoco'
}

version = project.version
group = project.group
archivesBaseName = project.archiveName

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
	mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.assertj:assertj-swing-junit:3.9.2'
}

sourceSets {
	main {
		java {
			srcDir "src"
		}
		resources {
		}
	}
	test {
		java {
			srcDir "test"
		}
	}
}

jar {
    manifest {
        attributes([
            "Specification-Title": project.name,
            "Specification-Vendor": "macaroni",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" : "macaroni",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Main-Class": "macaroni.Main"
        ])
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()

    maxHeapSize = '1G'

    testLogging {
        events "passed"
    }
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'pasta/**')
        }))
    }
}
